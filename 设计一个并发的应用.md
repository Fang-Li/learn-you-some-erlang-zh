# 设计一个并发的应用
骚年，你现在已经理解了Erlang的概念，但是再次提醒，从本书的开始到现在，所有的例子仅仅是玩具：比如 `calculators`, `trees`, `Heathrow to London` 等等这些例子。现在是时候开始来一些更有趣，并且更有教育意义的东西了。我们将要写一个小的并发程序，虽然小，但是很有用，并且是可扩展的。

哥是一个比较懒散的人，哥在家庭作业，写这本书，工作，参加会议，约会等等蛋疼的事情中忙迷糊了。我列出很多任务，但最终还是忘了去做。比如和妹子的约会。你希望有东西能提醒你将要做什么，好吧，作为码农，我们就用自己的手来写个事件提醒应用，来提醒你又有新番了，xx老师又有新作了，以及什么时候该和妹子约会了。

## 认识这个问题
首先你必须知道我们TMD的到底要干什么！ 你说：“约炮，哦不约会提醒程序嘛”。
擦，这是当然，但是你必须首先搞明白这些问题：

*   我们如何与这个应用交互
*   我们想让它为我们做什么
*   我们怎样用进程来描述这个问题
*   我们怎么知道该发送什么消息

如果所有种子都列出来了，那你下载AV是不是很容易？ 对，所以我们要规定这个应用的细则，全列出来，一一实现就可以。这个应用运行我们干这些事情：

*   添加事件。 事件包括截止时间，名字，描叙
*   快到一个事件的截止时间时，显示警告
*   可以根据事件名字取消此事件
*   没有持久化到硬盘。这对于真正的应用时不可取的，但在这里应用中我会告诉你哪儿可以插入你自定义的持久化代码
*   因为没有持久化数据，所以必须热更新代码
*   通过命令行和应用交互，但后续的扩展应该让它支持GUI, WEB, EMAIL等方式

这就是我准备要做的东西：

![structure][1]

`client`, `event server`, `x`, `y`, `z` 都是进程，它们能干什么：

#### Event Server
*   接受clients的订阅
*   向订阅了的clients发送通知
*   通过消息添加事件（如果必要就启动x,y,z进程）
*   通过消息取消事件，随后杀死事件进程
*   可以被client终止运行
*   可以在shell中热更新自身的代码

#### client
*   订阅Event Server，并且以消息的形式接收通知
*   通知server添加事件
*   通知server取消事件
*   监控server
*   关闭server

#### x, y and z
*   代表等待要发送的通知（它们仅仅是link 链接到event server的 timers）
*   当时间到的时候，给event server发送通知
*   接收到取消消息后，自杀

注意，所有的clients(IM, EMAIL 等我们并没有实现) 都会收到所有的事件通知，并且取消事件并不会通知到所有的clients。这个应用时写给我和你用的，假设只有一个用户在运行它。

这是一个更复杂的示例图，显示了所有可能的消息：

![complex graph][2]

这张图代表了所有的进程，箭头表示发送消息，我们已经完成了高阶协议，或者说一个框架。

要指出的是，在现实的应用中，一个event一个进程很难scale

> 这里没明白，求解释
> It should be noted that using one process per event to be reminded of is likely going to be overkill and hard to scale in a real world application. However

对于只有你一个用户的应用，这已经足够了。另一个方法是用 `timer:send_after` 这样的方法来避免spawn太多的进程。

## 定义协议
现在我们知道了每个组件要干什么，要怎么沟通。我们现在就把所有的消息全部列出来，并且说明这些消息应该是什么样子的。让我们从client和event server之间的交流开始。

![client and event server][3]

这里我用了两个`monitors`，因为client和event server之间并没有显式的从属关系。当然， 没有event server，client无法独立工作，但是没有client, server还可以存在。link 双向链接可以完成这个工作，但是因为我想让这个系统可扩展成很多clients（比如上面提到的IM, EMAIL），我们不能假设其他clients都希望跟着server一起挂，也不能假设所有的client都trap exit。 好，让我们继续看下面的消息

![add event][4]

向event server添加事件，如果一切正常，返回原子ok，否则 {error, Reason}。
对应的操作：取消事件，看下面的图

![cancel event][5]

当一个事件到期的时候，event server向client发送通知

![done notification][6]

我们还需要下面的两个特殊情况：关闭event server 或者 event server 崩溃

![shutdown][7]

event server挂的时候并没有直接向client发消息，因为`monitor`机制会通知我们的。这就是client和event server之间的消息，下面让我们看看event server和event processes之间的消息

首先要注意的是，我们要用link在event server和event process之间建立双向链接，因为event process必须跟着event server一起挂

再来看event process,当event server启动event process的时候，就给它一个唯一标示符（就是event的name）。当一个event的截止时间到了，它需要给server 发送消息:

![event done][8]

当然，event也必须处理来自event server 的 cancel请求

![event cancel][9]

最后一个消息是我们要从shell热更新event server

![code change][10]

这个操作不需要返回，当我们实际操作的时候，你就会明白为什么了。

当定义好了协议，并且设计了进程的层级关系后，就让我们开始这个项目吧。

## 基本目录结构
在开始之前，我们应该定义了一个标准的Erlang目录结构，它看起来像这样

    ebin/
    include/
    priv/
    src/

`ebin/` 文件夹用来放置你编译的输出的目标文件。`include`文件夹用来存放可能被其他application用到的公共`.hrl`文件，私有的`.hrl`文件还是直接放置到`src/`目录中。`priv`用来放置可以与erlang交互和可执行文件。这个项目不会使用到这个目录。最后就是`src/`目录，这是用来存放所有`.erl`源文件的。

在标准erlang项目中，这和目录结构可能会有不同的调整。比如`conf/`会用来放置配置文件，`doc/`用来放置文档，`lib/`用来放置第三方库。不同的Erlang项目可能还会使用其他名字的目录，但是上面提到的4个是通常都会有的。它们是 [standard OTP practices][11] 的一部分。

## An Event Module Event模块
在`src/`目录中，开始我们的`event.erl`模块，这个模块来实现先前提到的x,y,z事件进程。从这个模块开始是因为我们可以允许它，而不必非要实现gevent server或者client

在开始写代码以前，我得提醒现在的协议是不完整的。它表示了数据从一个进程发送到另一个进程，但：如何定位进程，使用`references` 或者 `name`， 或者其他。大部分消息都被包裹在 `{Pid, Ref, Message}` 格式中，`Pid` 是发送者pid, `Ref` 是唯一消息标示符，用于表示这个回复来自谁。如果在得到回复以前，我们发送了大量的消息，在没有`Ref`的情况下，我们将不知道那个消息回复了什么。

好吧，现在就开始，进程的核心就是运行`event.erl` 的 `loop/1` 函数：

    ```erlang
    loop(State) ->
        receive
            {Server, Ref, cancel} ->
                ...
        after Delay ->
            ...
        end.
    ```

这显示我们支持timeout超时，并且server发送cancel取消事件的方式。你应该注意到了 `loop/1` 的 `State` 参数。这个参数会包含 `timeout`, `event name`，等数据，它同时还得知道event server的pid，这样才可以给server发送消息。

这就是state要包含的全部数据，让我们申明一个state record吧：

    ```erlang
    -module(event).
    -compile(export_all).
    -record(state, {server,
                    name="",
                    to_to=0}).
    ```

有了state的定义，那就重写一下loop/1函数

    ```erlang
    loop(S = #state{server=Server}) ->
        receive
            {Server, Ref, cancel} ->
                Server ! {Ref, ok}
        after S#state.to_go*1000 ->
            Server ! {done, S#state.name}
    end.
    ```

把to_go 乘以 1000， 是为了把秒转成毫秒

> Don't drink too much Kool-Aid:
> 语音自身的坑! 
> 在函数首部直接绑定了Server变量，是是因为函数首部要进行模式匹配 `pattern matching`
> 在模式匹配中，`S#state.server` 会被默默的展开成`element(2, S)`，这并不是一个有效的用于匹配的模式。
> 但是 `s#state.to_go` 却能在 `after` 段里工作良好，是因为它会作为表达式进行求值

好了，现在让我们来测试loop

    ```erlang
    6> c(event).
    {ok,event}
    7> rr(event, state).
    [state]
    8> spawn(event, loop, [#state{server=self(), name="test", to_go=5}]).
    <0.60.0>
    9> flush().
    ok
    10> flush().
    Shell got {done,"test"}
    ok
    11> Pid = spawn(event, loop, [#state{server=self(), name="test", to_go=500}]).
    <0.64.0>
    12> ReplyRef = make_ref().
    #Ref<0.0.0.210>
    13> Pid ! {self(), ReplyRef, cancel}.
    {<0.50.0>,#Ref<0.0.0.210>,cancel}
    14> flush().
    Shell got {#Ref<0.0.0.210>,ok}
    ok
    ```

有大量的东西需要看。首先，我们通过`rr(Mod)`导入了`Mod`中的record,然后 spawn 了event:loop/1，把shell作为server，这个event在5秒后会发送消息。在3秒后进行第9条操作，6秒后进行第10条操作，你可以看到我们收到了 `{done, "test"}`消息。

紧接着，我测试了`cancel`功能。我创建了一个`reference`，发送消息，并且得到了一个包含同样`reference` 的消息，这样我就知道我收到的消息是来自于这个进程，而不是其他的进程。

`cancel`消息要包含一个`reference`，但`done`消息却没有，原因是我们不期望`done`消息来自某个特定的进程，（来自任何进程都可以，我们在接收的时候不会做匹配），并且收到`done`消息后，也不应该要回复它。我们再来一个测试，让一个事件在一年后过期会如何：

    ```erlang
    15> spawn(event, loop, [#state{server=self(), name="test", to_go=365*24*60*60}]).
    <0.69.0>
    16> 
    =ERROR REPORT==== DD-MM-YYYY::HH:mm:SS ===
    Error in process <0.69.0> with exit value: {timeout_value,[{event,loop,1}]}
    ```

开起来我们受到了限制。Erlang的 timeout 数值被限定在 50天以内 （毫秒）。这个测试并不是很有意义，但我展示这个测试是为了说明下面三点：

1.  在我写这篇文章的时候，这个问题确实让我蛋疼菊紧了一会
2.  Erlang当然不是对于每种任务都是完美的。
3.  其实这不是个问题，我们可以work around.

解决这个问题的办法是写个函数将timeout split成多个部分，就按照49天（Erlang timeout 的limit是50天）来分割：

    ```erlang
    %% Because Erlang is limited to about 49 days (49*24*60*60*1000) in
    %% milliseconds, the following function is used
    normalize(N) ->
        Limit = 49*24*60*60,
        [N rem Limit | lists:duplicate(N div Limit, Limit)].
    ```

函数 `lists:duplicate/2`需要两个参数， 它会把第二个参数重复第一个参数此. `[a, a, a] = lists:duplicate(3, a)`. 如果我们用 `98*24*60*60+4` 作为 `normalize/1`的参数，那么结果将是 `[4, 4233600, 4233600]` 。现在 `loop/1`应该是这个样子的：

    ```erlang
    %% Loop uses a list for times in order to go around the ~49 days limit
    %% on timeouts.
    loop(S = #state{server=Server, to_go=[T|Next]}) ->
        receive
            {Server, Ref, cancel} ->
                Server ! {Ref, ok}
        after T*1000 ->
            if Next =:= [] ->
                Server ! {done, S#state.name};
               Next =/= [] ->
                loop(S#state{to_go=Next})
            end
        end.
    ```

你可以试试，现在它可以正常工作了，支持任意的timeout。

很烦人的一点是每次启动event的时候，都要调用`event:normalize/1`函数，特别是不应该让使用者去了解这些细节。标准做法是用一个`init`函数来做event的初始化，和loop函数调用。现在我们来添加`start` 和 `start_link`这两个函数：

    ```erlang
    start(EventName, Delay) ->
        spawn(?MODULE, init, [self(), EventName, Delay]).

    start_link(EventName, Delay) ->
        spawn_link(?MODULE, init, [self(), EventName, Delay]).

    %%% Event's innards
    init(Server, EventName, Delay) ->
        loop(#state{server=Server,
                    name=EventName,
                    to_go=normalize(Delay)}). 
    ```

现在的接口清晰多了，在开始测试之前，我们把`cancel`函数也添加上

    ```erlang
    cancel(Pid) ->
        %% Monitor in case the process is already dead
        Ref = erlang:monitor(process, Pid),
        Pid ! {self(), Ref, cancel},
        receive
            {Ref, ok} ->
                erlang:demonitor(Ref, [flush]),
                ok;
            {'DOWN', Ref, process, Pid, _Reason} ->
                ok
        end.
    ```

新的技巧！ 这里我用了 `monitor` 来检测对应的process是否还存在。如果process已经挂掉了，我就可以避免无用的等待了，就直接返回ok。如果monitor返回一个reference，那么在我们发送cancel消息后，这个process也马上要挂了，所以我不在monitor它，以防止收到它挂的消息。注意，我用了`flush`选项，这将清除 `'DOWN'` 消息。如果这个`'DOWN'` 在我们还没来得及 `demonitor` 之前就收到了。

> 疑问： 为什么不用 `is_process_alive` 呢？

现在开始测试

    ```erlang
    17> c(event).
    {ok,event}
    18> f().
    ok
    19> event:start("Event", 0).
    <0.103.0>
    20> flush().
    Shell got {done,"Event"}
    ok
    21> Pid = event:start("Event", 500).
    <0.106.0>
    22> event:cancel(Pid).
    ok
    ```

工作正常！最后一件事情就是我们输入的过期时间。如果它能支持Erlang标准的datetime格式`{{Year, Month, Day}, {Hour, Minute, Second}}` 那就最好了。下面的函数将会干这个事情：

    ```erlang
    time_to_go(TimeOut={{_,_,_}, {_,_,_}}) ->
        Now = calendar:local_time(),
        ToGo = calendar:datetime_to_gregorian_seconds(TimeOut) -
               calendar:datetime_to_gregorian_seconds(Now),
        Secs = if ToGo > 0  -> ToGo;
                  ToGo =< 0 -> 0
               end,
        normalize(Secs).
    ```

为了让`init`函数使用这个 `time_to_go`， 最好把`Delay` 这个名字 换成 `DateTime`， 这样的代码才根据可描述性

    ```erlang
    init(Server, EventName, DateTime) ->
        loop(#state{server=Server,
                    name=EventName,
                    to_go=time_to_go(DateTime)}).
    ```


## The Event Server
现在让我们来实现 event server，根据上面的协议，event server的 骨架应该是这个样子的

    ```erlang
    -module(evserv).
    -compile(export_all).

    loop(State) ->
        receive
            {Pid, MsgRef, {subscribe, Client}} ->
                ...
            {Pid, MsgRef, {add, Name, Description, TimeOut}} ->
                ...
            {Pid, MsgRef, {cancel, Name}} ->
                ...
            {done, Name} ->
                ...
            shutdown ->
                ...
            {'DOWN', Ref, process, _Pid, _Reason} ->
                ...
            code_change ->
                ...
            Unknown ->
                io:format("Unknown message: ~p~n",[Unknown]),
                loop(State)
        end.
    ```

你应该注意到了，我将所有的消息都弄成了 `{Pid, Ref, Message}` 这种格式。现在 event server需要在 State中维护两个东西： 订阅的clients列表和spawn的event process列表。 如果你留意的话，应该注意到了，协议规定当一个event process完成了，event server就会收到 `{done, Name}`，但是server要给client发送的格式是 `{done, Name, Description}`。 这里的设计要素是让processes之间保持最小的信息交换，并且evnet process只持有它关系的数据。所以，event server 的 State这样定义：

    ```erlang
    -record(state, {events,    %% list of #event{} records
                    clients}). %% list of Pids

    -record(event, {name="",
                    description="",
                    pid,
                    timeout={{1970,1,1},{0,0,0}}}).
    ```

现在`loop/1` 应该的参数应该这样定义:

    ```erlang
    loop(S = #state{}) ->
        receive
            ...
        end.
    ```

`events` 和 `clients` 最好都是 `orddict`，我们不太可能同时拥有上百个它们。如果你回忆起来 [data structures][12] 这一章，`orddict` 是很适合我们现在的需求。 我们将写一个`init`函数来包裹`loop/1`

    ```erlang
    init() ->
        %% Loading events from a static file could be done here.
        %% You would need to pass an argument to init telling where the
        %% resource to find the events is. Then load it from here.
        %% Another option is to just pass the events straight to the server
        %% through this function.
        loop(#state{events=orddict:new(),
                    clients=orddict:new()}).
    ```

在这之后，我将根据上面的消息，一个一个的实现其处理方法。第一个消息是来自client的订阅消息。我们要保存所有订阅的clients，这样当一个事件到达过期时间，我们就可以通知所有的clients。并且上面协议也提到，我明应该`monitor`这些clients，显然，如果某个clients奔溃了，我们就不用给不存在的client发送消息了。

    ```erlang
    {Pid, MsgRef, {subscribe, Client}} ->
        Ref = erlang:monitor(process, Client),
        NewClients = orddict:store(Ref, Client, S#state.clients),
        Pid ! {MsgRef, ok},
        loop(S#state{clients=NewClients});
    ```

`loop/1` 首先`monitor` client, 然后将client通过key `Ref` 存入 orddict。这么做是因为： 当我们收到 `'EXIT'` 消息的时候，这个消息就包含有 reference。

下一个要注意的是，添加事件。现在可以见到的返回一个error status。这里唯一的验证就是检查给的过期时间。当然可以简单的通过与`{{_, _, _,}, {_, _, _}}` 来进行匹配。但是我没还得继续验证，比如非闰年的2月29号，或者 5小时，1分钟，75秒这种不存在的日期时间。我们用一个函数来验证输入的日期时间。

首先我们使用 [calendar:valid_data/1][12]. 这个函数用来检查日期是否正确。
但不幸的，`calendar`模块并没有提供检测时间`{H, M ,S}`是否正确的函数，所以我们得自己实现一个：

    ```erlang
    valid_datetime({Date,Time}) ->
        try
            calendar:valid_date(Date) andalso valid_time(Time)
        catch
            error:function_clause -> %% not in {{Y,M,D},{H,Min,S}} format
                false
        end;
    valid_datetime(_) ->
        false.

    valid_time({H,M,S}) -> valid_time(H,M,S).
    valid_time(H,M,S) when H >= 0, H < 24,
                           M >= 0, M < 60,
                           S >= 0, S < 60 -> true;
    valid_time(_,_,_) -> false.
    ```

现在 `valid_datatime/1` 函数可以用于添加事件的处理：

    ```erlang
    {Pid, MsgRef, {add, Name, Description, TimeOut}} ->
        case valid_datetime(TimeOut) of
            true ->
                EventPid = event:start_link(Name, TimeOut),
                NewEvents = orddict:store(Name,
                                          #event{name=Name,
                                                 description=Description,
                                                 pid=EventPid,
                                                 timeout=TimeOut},
                                          S#state.events),
                Pid ! {MsgRef, ok},
                loop(S#state{events=NewEvents});
            false ->
                Pid ! {MsgRef, {error, bad_timeout}},
                loop(S)
        end;
    ```

如果time是合法的，我们就spawn一个event process，并且把这个event的信息保存在server 的state中，最后给client一个确认。如果timeout是错误的，我们就通知client bad_timeout，并不会静默的吞掉错误，或者直接崩溃。当然，还有些附加的检测，比如event name是否冲突等。

下一个要处理的消息就是取消事件。对于client来说，取消事件永远不会失败。所以server仅仅检查要取消的event是否在 server的state中，如果在，就调用 `event:cancel/1`，并且返回`ok`。如果不存在，直接返回`ok`

    ```erlang
    {Pid, MsgRef, {cancel, Name}} ->
        Events = case orddict:find(Name, S#state.events) of
                     {ok, E} ->
                         event:cancel(E#event.pid),
                         orddict:erase(Name, S#state.events);
                      error ->
                         S#state.events
                 end,
        Pid ! {MsgRef, ok},
        loop(S#state{events=Events});
    ```

很好，到目前为止，所有的client和server的交互都完成了。让我们来处理server和events process之间的消息发送。有两个消息要处理：取消一个event，一个event到期结束。到期结束就是简单的返回{done, Name}.

    ```erlang
    {done, Name} ->
        case orddict:find(Name, S#state.events) of
            {ok, E} ->
                send_to_clients({done, E#event.name, E#event.description},
                                S#state.clients),
                NewEvents = orddict:erase(Name, S#state.events),
                loop(S#state{events=NewEvents});
            error ->
                %% This may happen if we cancel an event and
                %% it fires at the same time
                loop(S)
        end;
    ```

`send_to_client/2` 做的事情就如同其名字：

    ```erlang
    send_to_clients(Msg, ClientDict) ->
        orddict:map(fun(_Ref, Pid) -> Pid ! Msg end, ClientDict).
    ```

最后还剩下的就是 clients down， shutdown, code upgrades

    ```erlang
    shutdown ->
        exit(shutdown);
    {'DOWN', Ref, process, _Pid, _Reason} ->
        loop(S#state{clients=orddict:erase(Ref, S#state.clients)});
    code_change ->
        ?MODULE:loop(S);
    Unknown ->
        io:format("Unknown message: ~p~n",[Unknown]),
        loop(S)
    ```

`shutdown` 很明确，收到这个消息，就让kill自身。如果你想把state持久化到磁盘。那就在这里做这个事情。如果你想要更安全的 save/exit，那么就要在每个 `add`, `cancel` `done` 处做处理。 加载磁盘上的数据，可以放到`init`方法中。

`'DOWN'` 消息表示一个client挂了，那么我们就从clients list中移除这个client

Unknown 消息直接用io:format打印出来，用于debug，当然真正的产品级应用程序可能会更倾向于使用专门的loggin模块。

现在到了代码热更新了，这个很有趣，所以我要单独一节来讲它。

[1]: http://learnyousomeerlang.com/static/img/reminder-structure.png
[2]: http://learnyousomeerlang.com/static/img/reminder-bubbles-and-arrows.png
[3]: http://learnyousomeerlang.com/static/img/reminder-subscribe.png
[4]: http://learnyousomeerlang.com/static/img/reminder-add.png
[5]: http://learnyousomeerlang.com/static/img/reminder-remove.png
[6]: http://learnyousomeerlang.com/static/img/reminder-cs-done.png
[7]: http://learnyousomeerlang.com/static/img/reminder-shutdown.png
[8]: http://learnyousomeerlang.com/static/img/reminder-es-done.png
[9]: http://learnyousomeerlang.com/static/img/reminder-cancel.png
[10]: http://learnyousomeerlang.com/static/img/reminder-code-change.png
[11]: http://www.erlang.org/doc/design_principles/applications.html#id71171
[12]: http://erldocs.com/R15B/stdlib/calendar.html#valid_date/1